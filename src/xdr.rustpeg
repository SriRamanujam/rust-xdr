use super::{Value, Type, Decl, Defn, EnumDefn, UnionCase};

endkw    = &(sp / symbol) __

// Keywords
bool     = "bool" endkw
unsigned = "unsigned" endkw
int      = "int" endkw
hyper    = "hyper" endkw
float    = "float" endkw
double   = "double" endkw
quadruple= "quadruple" endkw
struct   = "struct" endkw
enum     = "enum" endkw
union    = "union" endkw
typedef  = "typedef" endkw
switch   = "switch" endkw
case     = "case" endkw
default  = "default" endkw
const    = "const" endkw
opaque   = "opaque" endkw
string   = "string" endkw
void     = "void" endkw

// Symbols
lbrace   = "{" __
rbrace   = "}" __
lbrack   = "[" __
rbrack   = "]" __
lparen   = "(" __
rparen   = ")" __
lt       = "<" __
gt       = ">" __
star     = "*" __
colon    = ":" __
semi     = ";" __
comma    = "," __
eq       = "=" __

keyword
    = bool
    / unsigned
    / int
    / hyper
    / float
    / double
    / quadruple
    / struct
    / enum
    / union
    / typedef
    / switch
    / case
    / default
    / const
    / opaque
    / string
    / void

symbol
    = lbrace
    / rbrace
    / lbrack
    / rbrack
    / lparen
    / rparen
    / lt
    / gt
    / star
    / colon
    / semi
    / comma
    / eq

identifier -> String
    = !keyword id:valid_id __		        { id }

valid_id -> String
    = [a-zA-Z_][a-zA-Z0-9_]*                    { match_str.to_string() }

constant -> i64 = n:number __			{ n }

number -> i64
    = "-"? [0-9]+                               { match_str.parse().unwrap() }

declaration -> Decl
    = t:type_spec id:identifier lbrack s:value rbrack  { Decl::Array(id, t, s) }
    / t:type_spec id:identifier lt s:value? gt   { Decl::Flex(id, t, s) }
    / t:type_spec star id:identifier             { Decl::Option(id, t) }
    / t:type_spec id:identifier                  { Decl::Scalar(id, t) }
    / opaque id:identifier lbrack s:value rbrack { Decl::Array(id, Type::Opaque, s) }
    / opaque id:identifier lt s:value? gt        { Decl::Flex(id, Type::Opaque, s) }
    / string id:identifier lt s:value? gt        { Decl::Flex(id, Type::String, s) }
    / void                                      { Decl::Void }

value -> Value
    = c:constant                { Value::Const(c) }
    / id:identifier             { Value::Ident(id) } // XXX lookup const

type_spec -> Type
    = unsigned int              { Type::UInt }
    / unsigned hyper            { Type::UHyper }
    / int                       { Type::Int }
    / hyper                     { Type::Hyper }
    / float                     { Type::Float }
    / double                    { Type::Double }
    / quadruple                 { Type::Quadruple }
    / bool                      { Type::Bool }
    / e:enum_type_spec          { e }
    / s:struct_type_spec        { s }
    / u:union_type_spec         { u }
    / id:identifier             { Type::Ident(id) }

enum_type_spec -> Type
    = enum b:enum_body          { Type::Enum(b) }

enum_body -> Vec<EnumDefn>
    =  lbrace e:enum_assign ++ comma rbrace     { e }

enum_assign -> EnumDefn
    = id:identifier v:enum_val? { EnumDefn(id, v) }

enum_val -> Value
    = eq v:value		{ v }
    
struct_type_spec -> Type
    = struct s:struct_body      { Type::Struct(s) }

struct_body -> Vec<Decl>
    = lbrace s:struct_decl+ rbrace    { s }

struct_decl -> Decl
    = d:declaration semi         { d }

union_type_spec -> Type
    = union u:union_body      { Type::union(u) }

union_body -> (Decl, Vec<UnionCase>, Option<Decl>)
    = switch lparen d:declaration rparen lbrace c:union_case+ dfl:union_default? rbrace   { (d, c, dfl) }

union_case -> UnionCase
    = case v:value colon d:declaration semi      { UnionCase(v, d) }

union_default -> Decl
    = default colon d:declaration semi           { d }

const_def -> Defn
    = const id:identifier eq v:constant semi    { Defn::Const(id, v) }

type_def -> Decl
    = typedef d:declaration semi               { d }
    / enum id:identifier e:enum_body semi      { Decl::Scalar(id, Type::Enum(e)) }
    / struct id:identifier s:struct_body semi  { Decl::Scalar(id, Type::Struct(s)) }
    / union id:identifier u:union_body semi    { Decl::Scalar(id, Type::union(u)) }

definition -> Defn
    = t:type_def        { Defn::Typedef(t) }
    / c:const_def       { c }

#[pub]
specification -> Vec<Defn>
    = directive __ d:definition*     { d }

sp = (whitespace / (eol directive) / comment)
__ = sp*

comment = "/*" (!"*/" .)* "*/"
eol = "\n" / "\r\n" / "\r" / "\u{2028}" / "\u{2029}"
whitespace = [ \t\u{00A0}\u{FEFF}\u{1680}\u{180E}\u{2000}-\u{200A}\u{202F}\u{205F}\u{3000}]

directive = (passthrough / cpreproc)*

cpreproc = "#" line eol

passthrough
    = passline				    { }

passline
    = "%" whitespace* line:line eol         { println!("line {:?}", line) }

line -> String
    = (!eol .)*				    { match_str.to_string() }
